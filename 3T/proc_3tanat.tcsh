#! /bin/csh
# Use UAC image generated by @SSwarper to align with epi and do recon
# use @SSwarper to process anatomy
# use input as sub
if ( $# > 0 ) then
set sub = $1
set datafolder=/Volumes/WD_F/gufei/3T_cw/${sub}
cd "${datafolder}"
# make dir to save masks
if (! -e mask) then
    mkdir mask
endif
# align to epi data
# use matrix cauculated previously
3dAllineate                                                                                         \
    -master ${sub}.de.results/anatSS.${sub}+orig                        \
    -1Dmatrix_apply ${sub}.de.results/anatSS.${sub}_al_keep_mat.aff12.1D                \
    -input ${sub}_anat_warped/anatUAC.${sub}.nii                                                  \
    -prefix ${sub}_anat_warped/anatUACa.${sub}.nii

# use freesurfer to reconstruct surfaces
# -sd pass working dir, the default is defined by env var 
# high resolution reconstruction
recon-all                                                                                   \
    -i ${sub}_anat_warped/anatUACa.${sub}.nii                                                  \
    -s ${sub}_surf_hiresalign -sd ./                                                                 \
    -all                                                                                        \
    # -parallel -threads 8

# create files for suma
# -fs_setup might me useful on macOS according to the help page
@SUMA_Make_Spec_FS -fs_setup -NIFTI -fspath ${sub}_surf_hiresalign -sid ${sub}

# Amygdala segmentation
# use multipal threads
setenv ITK_GLOBAL_DEFAULT_NUMBER_OF_THREADS 8
# the second input is subject_dir, error will occur when using ./
segmentHA_T1.sh ${sub}_surf_hiresalign ${datafolder}
# save amygdala mask
set masks = `ls ${sub}_surf_hiresalign/mri/?h.hippoAmygLabels-T1.v21.HBT.mgz`
# echo ${masks}
foreach mask (${masks})
    # echo ${mask}
    # change name
    set name = `echo ${mask} | cut -d '/' -f 3 | sed 's/.mgz/.nii/'`
    # echo ${name}
    mri_convert -ot nii ${mask} ${sub}_surf_hiresalign/SUMA/${name}
end

# generate surface model
# left hemisphere
mris_convert ${sub}_surf_hiresalign/surf/lh.pial ${sub}_surf_hiresalign/lh.pial.stl
# right hemisphere
mris_convert ${sub}_surf_hiresalign/surf/rh.pial ${sub}_surf_hiresalign/rh.pial.stl

# # check alignment in SUMA folder
# afni -niml
# suma -spec ${sub}_surf_hiresalign/SUMA/${sub}_both.spec -sv ${sub}_surf/SUMA/${sub}_SurfVol.nii

# set results directory
# use a different directory to avoid conflicts with existing files in de
set analysis=de
set subj = ${sub}.${analysis}
cd ${subj}.results

# align exp anatomy to suma surfvolume
# use wd to apply 12 parameters affine transform
# surf_anat is in different grids and has skull
# an error will occur if set prefix in -skull_strip_opt
@SUMA_AlignToExperiment                                                 \
    -exp_anat anat_final.${subj}+orig.HEAD                              \
    -surf_anat ../${sub}_surf_hiresalign/SUMA/${sub}_SurfVol.nii             \
    -prefix surf_hiresalign_align.${subj}                                    \
    -strip_skull surf_anat                                              \
    -wd                                                                 \
    -align_centers
# change segmentation masks to experiment space
# can also use -surf_anat_followers option in @SUMA_AlignToExperiment 
foreach mask (${masks})
    set input = `echo ${mask} | sed 's/mri/SUMA/' | sed 's/.mgz/.nii/'`
    set output = `echo ${input} | cut -d '/' -f 3 | sed 's/.nii/+orig/'`
    # resample to the EPI grid
    3dAllineate                                                             \
        -master vr_base_min_outlier+orig                                          \
        -1Dmatrix_apply surf_hiresalign_align.${subj}.A2E.1D                     \
        -input ../${input}                                                  \
        -prefix ${output}                                                   \
        -final NN
    set lr=`echo ${output} | cut -c1`
    3dcalc -a "${output}<7001..7015>" -expr 'step(a-7000)' -prefix ${lr}Amy.freesurfer+orig   
    3dcalc -a "${output}" -expr 'ispositive(a-7000)*(a-7000)' -prefix ${lr}Amy.seg.freesurfer+orig   
end

# all of the amygdala
3dcalc -a lAmy.freesurfer+orig -b rAmy.freesurfer+orig -expr 'a+b' -prefix Amy.freesurfer+orig
# different regions of amygdala
3dcalc -a lAmy.seg.freesurfer+orig -b rAmy.seg.freesurfer+orig -expr 'a+b' -prefix Amy.seg.freesurfer+orig
# copy Amy9_align masks
3dcopy Amy.freesurfer+orig ../mask/Amy9_align.freesurfer+orig
# copy Amy.seg masks to mask dir as sAmy
3dcopy Amy.seg.freesurfer+orig ../mask/sAmy.freesurfer+orig

# Print number of voxels for each ROI
3dROIstats -nzvoxels -mask Amy.seg.freesurfer+orig.HEAD Amy.seg.freesurfer+orig.HEAD >! ../mask/voxels.txt

# create cortical amygdala mask
3dcalc -a Amy.seg.freesurfer+orig -expr 'amongst(a,7,9)' -prefix ../mask/corticalAmy_align.freesurfer+orig
# create centralmedial amygdala mask
3dcalc -a Amy.seg.freesurfer+orig -expr 'amongst(a,5,6)' -prefix ../mask/CeMeAmy_align.freesurfer+orig
# create lateralbasal amygdala mask
3dcalc -a Amy.seg.freesurfer+orig -expr 'amongst(a,1,3,8,15)' -prefix ../mask/BaLaAmy_align.freesurfer+orig
# creat amygdala mask without AAA
3dcalc -a Amy.seg.freesurfer+orig -expr 'amongst(a,1,3,5,6,7,8,9,15)' -prefix ../mask/Amy8_align.freesurfer+orig

# create mask for each region
foreach region (1 3 5 6 7 8 9 10 15)
    3dcalc -a Amy.seg.freesurfer+orig -expr "equals(a,${region})" -prefix ../mask/Amy_align${region}seg.freesurfer+orig
end

else
 echo "Usage: $0 <Subjname>"

endif
